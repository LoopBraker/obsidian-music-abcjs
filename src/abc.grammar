@top Program { (line newline)* line? }

@external tokens lineStartTokens from "./tokens" { InfoKey }

line { 
  // 1. Specific Headers (Priority)
  CommentedDirective | 
  MidiLine |    // %%MIDI
  DirectiveLine | // %%
  VoiceLine |   // V:
  KeyLine | // K:    
  InfoLine |
  TimeSignatureLine | // M:
  
  // 2. Comments
  Comment | 
  
  // 3. Score (Music) - The Default Fallback
  // If it's not a header or comment, it's music.
  ScoreLine |
  
  // 4. Garbage collector (for safety)
  Other 
}

// === SCORE (MUSIC) ===
// A line of music. It doesn't need a starting keyword.
ScoreLine {
  ( ScoreContent | Space )+ 
  InlineComment?
}

ScoreContent {
  BarComponent |
  Annotation |
  Ornament |
  Duration |
  ComplexNote | 
  SimpleNoteCapital | 
  SimpleNoteLower | 
  Rest 
  // Spacing |
  // MidiNumber |   
  // Slash |       
  // Word | 
  // SingleChar
}

// === MIDI ===
MidiLine { MidiKeyword Space MidiContent (Space MidiContent)* InlineComment? }
MidiContent {
  ProgramAssignment | ChordProgAssignment | ChannelAssignment |
  DrumAssignment | GchordAssignment | TransposeAssignment |
  DrumOnKeyword | DrumOffKeyword | DirectiveArgs | GchordOnKeyword | GchordOffKeyword
}

//%%MIDI program [Voice number] n
ProgramAssignment { MidiProgram Space (SingleDigit Space Digit)? }
MidiProgram { @specialize<Word, "program"> }

ChordProgAssignment { @specialize<Word, "chordprog"> Space MidiNumber }
ChannelAssignment { @specialize<Word, "channel"> Space MidiNumber }
// -------------------------------------------------------
// %%MIDI drum sequence [drum programs] [drum velocities]
DrumAssignment { Drum Space DrumSequence Space InstrumentVolume* }
Drum { @specialize<Word, "drum"> }
InstrumentVolume { @specialize<Word, "instrument"> Space MidiNumber }
// The sequence may contain 'd' for a drum strike or 'z' for a rest. The standard note length of a single note d is not set by the L: command. Instead it is adjusted so that the entire drum sequence fits exactly into one bar.In other words the duration of each note is divided by the total duration of the sequence.This means that, for example, the drum sequence "dd" is equivalent to drum string "d4d4". You cannot currently specify fractions directly (eg. C3/2) as done in the body of the music, but it is still possible to express complex rhythms. For example, to indicate a rhythm such as (3ddd d/d/d/d, you would write the string "d4d4d4d3d3d3d3".
DrumSequence { (DrumStrike | Rest | Digit)* }

GchordAssignment { @specialize<Word, "gchord"> Space DirectiveArgs }
TransposeAssignment { @specialize<Word, "transpose"> Space MidiNumber }
DrumOnKeyword { @specialize<Word, "drumon"> }
DrumOffKeyword { @specialize<Word, "drumoff"> }
GchordOnKeyword { @specialize<Word, "gchordon"> }
GchordOffKeyword { @specialize<Word, "gchordoff"> }

// === Voice ===
VoiceLine { VoiceKey Space? VoiceName (Space VoiceProperty)* InlineComment? }

VoiceName { 
  // 1. Allow text followed immediately by a number (e.g. "V1", "Violin2")
    (Word | SimpleNoteCapital | SimpleNoteLower | SingleChar | ComplexNote) (SimpleNoteCapital | SimpleNoteLower | ComplexNote | Word)? (Duration | MidiNumber)? |
  
  // 2. Fallbacks for other single types
  MidiNumber | Slash | BarComponent | Annotation | Duration
}
VoiceProperty { GenericAssignment | textIdentifier}

// === Key ===
KeyLine { KeyKey Space? KeyTonic (KeyProperty Space)* InlineComment? }

KeyTonic {
  // Option 1: "none" or "HP"
  None |
  // Option 2: Standard Key
  // Matches C, F#, Bb, Am (if written as word)
  SimpleNoteCapital Space? (Sharp)? Space? KeyMode? Space
}

KeyMode {
   @specialize<Word, "min" | "minor" | 
                     "maj" | "major" | 
                     "dor" | "dorian" | 
                     "phr" | "phrygian" | 
                     "lyd" | "lydian" | 
                     "mix" | "mixolydian" | 
                     "loc" | "locrian" | 
                     "aeo" | "aeolian" | 
                     "ion" | "ionian"> |
    @specialize<SingleChar, "m">
}

KeyProperty { GenericAssignment | textIdentifier }

// === Info Fields ===
// === Generic Info ===
InfoLine { InfoKey ( Space? InfoVal )* InlineComment? }
InfoVal { textIdentifier | MidiNumber | Slash | BarComponent}

TimeSignatureLine { 
  TimeSignatureKey Space? CommonTimeSignatures InlineComment? 
}


// === Helpers ===
GenericAssignment { 
  AssignmentKey equalsWithSpaces AssignmentValue 
}

AssignmentKey { 
  textIdentifier 
}

AssignmentValue { 
  textIdentifier | MidiNumber 
}

textIdentifier { 
  Word | SimpleNoteCapital | SimpleNoteLower | Rest | Spacing | SingleChar | Annotation | Duration | ComplexNote | Ornament 
}

// === Directives ===
DirectiveLine { 
  DirectiveKeyword ( Space DirectiveArgs? )? InlineComment? 
}

@tokens {
  newline { "\n" }
  // Directives and Keywords
  MidiKeyword { "%%MIDI" }
  DirectiveKeyword { "%%" ![ \t%\n]+ }
  VoiceKey { "V:" }
  KeyKey { "K:" }
  TimeSignatureKey { "M:" }

  None { "none" }
  CommonTimeSignatures{ None | "4/4" | "3/4" | "2/4" | "6/8" | "12/8" | "2/2" | "C" | "C|"}

  Sharp { "#" }
  Digit { @digit+ }
  SingleDigit { @digit }

  BarComponent { "|" | ":" | "[" | "]" }
  
  // Matches a double quote, followed by anything that ISN'T a newline or quote, ending with a quote.
  Annotation { '"' ![\n"]* '"' }

  // Duration
  Duration { "<" | ">" | "-" | "(" | ")" | ".(" |  ":" | "{" | "}" | "(" $[\t ]* @digit+  | 
            ( @digit + ) | ( "/" + @digit* ) | ( @digit+ "/" @digit* )}
  
  Ornament{ "~" | "." | "u" | "v" }

  // Complex Note
  ComplexNote {
    ( ("^" "^"? | "_" "_"? | "=") $[A-Ga-g] ("'" | ",")* ) | 
    ( $[A-Ga-g] ("'" | ",")+ ) 
  }

  //Compex Note together (e.g. C,,E,, etc). Not spaced.
  ComplexNoteTogether {
    ComplexNote (ComplexNote)+
  }

  // Word (2+ chars)
  //Word { (@asciiLetter | "+" | "-" | @digit) (@asciiLetter | "+" | "-" | @digit)+ }

   Word { 
    @asciiLetter @asciiLetter (@asciiLetter | "+" | "-" | "_" | @digit)* 
  }

  // SimpleNote (1 char note)
  SimpleNoteCapital { $[A-G] }
  SimpleNoteLower { $[a-g] }

  // Rests and Spacing: zZxy
  Rest { $[zZ] }
  Spacing { $[xXy] }

  // SingleChar (1 char fallback)
  SingleChar { @asciiLetter }

  Slash { "/"}
  
  Space { $[\t ]+ }

  equalsWithSpaces { ($[\t ]+ "=" $[\t ]*) | ("=" $[\t ]*) }

  //MIDI
  MidiNumber { ("+" | "-")? @digit+ }
  DrumStrike { "d" }

  DirectiveArgs { ![\s%\n=] ![%\n]* }
  CommentedDirective { "%%%" ![\n]* }
  InlineComment { "%" ![\n]* }
  Comment { "%" ![\n]* }
  Other { ![\n]+ } //  THis 
  
  @precedence { 
    equalsWithSpaces,
    CommentedDirective,
    MidiKeyword,
    DirectiveKeyword, 
    VoiceKey,
    KeyKey,
    TimeSignatureKey,
    BarComponent,
    CommonTimeSignatures,
    Annotation,
    Ornament,
    Duration,
    ComplexNote,
    Word,
    SimpleNoteCapital,
    SimpleNoteLower,
    Rest,
    Spacing,
    SingleChar,
    Slash,
    Sharp,
    MidiNumber,
    Space,
    DirectiveArgs,
    Comment, 
    Other 
  }
}